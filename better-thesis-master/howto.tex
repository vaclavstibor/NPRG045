\chapter{SPARQL Wrapper}
\label{app:sparql-wrapper}
To ensure consistency in the text when comparing results with the naive approach methods, we will refer to the QID (\textit{?id}) and Label (\textit{?idLabel}) related to the Organization entity, as well as Ticker (\textit{?ticker}) and Stock Exchange (\textit{?exchangeLabels}) in the query result tables.

\section{Query 1: Direct ticker retrieval}
\label{appsec:q1-direct-ticker-retrieval}

\begin{lstlisting}[language=SPARQL, caption={SPARQL Query 1: Retrieve entity information for entities directly with the \textit{stock exchange} property.}, label={lst:sparql_query_1}]
    SELECT DISTINCT 
      ?id             # Selects the entity ID
      ?idLabel        # Selects the label of the entity
      ?exchangesLabel # Selects the label of the exchange
      ?ticker         # Selects the ticker symbol
    
    WHERE {
      # Retrieves labels in English
      SERVICE wikibase:label {
        bd:serviceParam wikibase:language 
            "[AUTO_LANGUAGE],en".
      }
    
      # Specifies the QIDs of the entities
      VALUES ?id { 
        wd:Q11463    # Adobe
        wd:Q3884     # Amazon
        wd:Q95       # Google
        wd:Q37156    # IBM
        wd:Q18811574 # Meta
        wd:Q2283     # Microsoft
        wd:Q48938223 # TikTok
        wd:Q21708200 # OpenAI
        wd:Q209330   # Instagram
      }
    
      # Specifies the QIDs of the stock exchanges
      VALUES ?exchanges { 
        wd:Q82059    # NASDAQ
        wd:Q13677    # NYSE
      }
    
      # Matches entities with stock exchange property
      ?id p:P414 ?exchange.
    
      # Filters the exchanges to those specified 
      # and retrieves the ticker symbol
      ?exchange ps:P414 ?exchanges; 
               pq:P249 ?ticker.

      # Filters tickers without an end time
      FILTER NOT EXISTS {
          ?exchange pq:P582 ?endTime.
      }
    }
\end{lstlisting}

\section{Query 2: Owner-based ticker retrieval}
\label{appsec:q2-owner-based-ticker-retrieval}

\begin{lstlisting}[language=SPARQL, caption={SPARQL Query 2: Retrieve entity information for remaining entities with the \textit{owned by} property.}, label={lst:sparql_query_2}]
    SELECT DISTINCT 
      ?id             # Selects the entity ID
      ?idLabel        # Selects the label of the entity
      ?exchangesLabel # Selects the label of the exchange
      ?ticker         # Selects the ticker symbol
      
    WHERE {
      # Retrieves labels in English
      SERVICE wikibase:label {
        bd:serviceParam wikibase:language 
            "[AUTO_LANGUAGE],en".
      }
    
      # Specifies the QIDs of the remaining entities
      VALUES ?id { 
        wd:Q95       # Google
        wd:Q18811574 # Meta
        wd:Q48938223 # TikTok
        wd:Q21708200 # OpenAI
        wd:Q209330   # Instagram        
       }
    
      # Specifies the QIDs of the stock exchanges
      VALUES ?exchanges { 
        wd:Q82059    # NASDAQ
        wd:Q13677    # NYSE
      }
    
      # Matches entities with owner property
      ?id wdt:P127 ?owner.
      ?owner p:P414 ?exchange.
    
      # Filters the exchanges to those specified 
      # and retrieves the ticker symbol
      ?exchange ps:P414 ?exchanges; 
               pq:P249 ?ticker.

      # Filters tickers without an end time
      FILTER NOT EXISTS {
          ?exchange pq:P582 ?endTime.
      }
    }
\end{lstlisting}

\section{Query 3: Differentiated ticker retrieval}
\label{appsec:q3-differentiated-ticker-retrieval}

\begin{lstlisting}[language=SPARQL, caption={SPARQL Query 3: Retrieve entity information for remaining entities with the \textit{different from} property.}, label={lst:sparql_query_3}]
	SELECT DISTINCT 
	  ?id             # Selects the entity ID
	  ?idLabel        # Selects the label of the entity
	  ?exchangesLabel # Selects the label of the exchange
	  ?ticker         # Selects the ticker symbol
	  
	WHERE {
	  # Retrieves labels in English
	  SERVICE wikibase:label {
		bd:serviceParam wikibase:language 
			"[AUTO_LANGUAGE],en".
	  }
	
	  # Specifies the QIDs of the remaining entities
	  VALUES ?id { 
		wd:Q18811574 # Meta
		wd:Q48938223 # TikTok
		wd:Q21708200 # OpenAI
	  }
	
	  # Specifies the QIDs of the stock exchanges
	  VALUES ?exchanges { 
		wd:Q82059    # NASDAQ
		wd:Q13677    # NYSE
	  }
	
	  # Matches entities with the 'different from' property
	  ?id wdt:P1889 ?differs.
	  ?differs p:P414 ?exchange.
	
	  # Filters the exchanges to those specified 
	  # and retrieves the ticker symbol
	  ?exchange ps:P414 ?exchanges; 
			   pq:P249 ?ticker.
			   
	  # Filters tickers without an end time
	  FILTER NOT EXISTS {
		  ?exchange pq:P582 ?endTime.
	  }
	}
  \end{lstlisting}


